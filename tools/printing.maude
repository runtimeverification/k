***(
    This file is part of the K-Maude tool

    Copyright (C) 2009-2010 Traian Florin Șerbănuță

    This file is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307, USA.
***)
fmod MOD-NAME-TO-QID is
  pr META-MODULE-EXTRAS .
  pr QID-EXTRAS .

  op header2Qid :  -> Qid .
  op header2QidList : Header -> QidList .
  op parameterDecl2Qid : ParameterDecl -> Qid .
  op parameterDecl2QidList : ParameterDecl -> QidList .
  op parameterDeclList2Qid : ParameterDeclList -> Qid .
  op parameterDeclList2QidList : ParameterDeclList -> QidList .

  vars QI X : Qid .
  var  QIL : QidList .
  vars ME ME' : ModuleExpression .
  var  PDL : ParameterDeclList .
  var  PD : ParameterDecl .

  eq header2Qid(QI) = QI .
  eq header2Qid(nullHeader) = ' .
---  eq header2Qid(pd(X :: ME)) = qidList2Qid(header2QidList(pd(X :: ME))) .
---  eq header2QidList(pd(X :: ME)) = X ':: header2QidList(ME) .

  eq header2QidList(QI) = QI .
  eq header2QidList(nullHeader) = ' .

  eq header2Qid((ME { PDL })) = qidList2Qid(header2QidList((ME { PDL }))) .
  ceq header2QidList((ME { PDL })) 
    = (if QI == '\s then QIL else QIL QI fi 
       '`{ parameterDecl2QidList(PDL) '`} ) 
    if QIL QI := header2QidList(ME) .
 
  eq parameterDecl2Qid(X :: ME) = qidList2Qid(X ':: header2Qid(ME)) .

  eq parameterDeclList2Qid(PDL)
    = qidList2Qid(parameterDeclList2QidList(PDL)) .

  eq parameterDeclList2QidList(X :: ME) = X ':: header2QidList(ME) .
  eq parameterDeclList2QidList((X :: ME, PDL))
    = parameterDeclList2QidList(X :: ME) '`, parameterDeclList2QidList(PDL)
    [owise] .
endfm


fmod DECL-META-PRETTY-PRINT is
  pr META-MODULE-EXTRAS .
  pr META-LEVEL .
  pr CONVERSION .
  pr NAT-LIST .
  pr QID-EXTRAS .

  ---- name of a sort (the name of S{P1, ..., Pn} is S)
  op getName : Sort -> Qid .
  eq getName(S:Sort) 
    = if findOpening(string(S:Sort), "{", "}", sd(length(string(S:Sort)), 2))
           == notFound
      then S:Sort
      else qid(substr(string(S:Sort), 
                      0, 
                      findOpening(string(S:Sort), 
                        "{", "}", 
                        sd(length(string(S:Sort)), 2))))
      fi .

  ---- parameters of a sort (the parameters of S{P1, ..., Pn} are P1 ... Pn)
  op getPars : Sort -> ParameterList [memo] .
  op getParsAux : String Nat Nat -> ParameterList .

  eq getPars(S:Sort) 
    = if findOpening(string(S:Sort), "{", "}", sd(length(string(S:Sort)), 2))
           == notFound
      then empty
      else getParsAux(string(S:Sort), 
             findOpening(string(S:Sort), 
               "{", "}", sd(length(string(S:Sort)), 2)) + 1,
             length(string(S:Sort)))
      fi .

  var  St Pattern OpenPar ClosingPar : String .
  vars Lt R N OpenPars ClosingPars : Nat .

  eq getParsAux(St, Lt, R)
    = if findOut(St, ",", "{", "}", Lt) == notFound
      then qid(substr(St, Lt, sd(findClosing(St, "{", "}", Lt), Lt)))
      else (qid(substr(St, Lt, sd(findOut(St, ",", "{", "}", Lt), Lt))),
            getParsAux(St, findOut(St, ",", "{", "}", Lt) + 1, R))
      fi .

  ---- finds a pattern out of balanced parentheses 
  ---- findOut("S{P1, P2{P21, P22}, P3}", ",", "{", "}", 6) returns 18, not 12
  op findOut : String String String String Nat -> FindResult .
  op findOut : String String String String Nat Nat -> FindResult .

  eq findOut(St, Pattern, OpenPar, ClosingPar, N)
    = findOut(St, Pattern, OpenPar, ClosingPar, 0, N) .

  eq findOut(St, Pattern, OpenPar, ClosingPar, OpenPars, N)
    = if N >= length(St)
      then notFound
      else if OpenPars == 0 and-then substr(St, N, length(Pattern)) == Pattern
           then N
           else if substr(St, N, length(OpenPar)) == OpenPar
                then findOut(St, Pattern, OpenPar, ClosingPar, 
                       OpenPars + 1, N + 1)
                else if substr(St, N, length(ClosingPar)) == ClosingPar
                     then findOut(St, Pattern, OpenPar, ClosingPar, 
                            sd(OpenPars, 1), N + 1)
                     else findOut(St, Pattern, OpenPar, ClosingPar, 
                            OpenPars, N + 1)
                     fi
                fi
           fi
      fi .

  ---- finds the first closing unbalanced parenthesis 
  ---- findOut("P1, P2{P21, P22}, P3}", "{", "}", 6) returns 21, not 16
  op findClosing : String String String Nat -> FindResult .
  op findClosing : String String String Nat Nat -> FindResult .

  eq findClosing(St, OpenPar, ClosingPar, N)
    = findClosing(St, OpenPar, ClosingPar, 0, N) .

  eq findClosing(St, OpenPar, ClosingPar, OpenPars, N)
    = if N >= length(St)
      then notFound
      else if OpenPars == 0 
              and-then substr(St, N, length(ClosingPar)) == ClosingPar
           then N
           else if substr(St, N, length(OpenPar)) == OpenPar
                then findClosing(St, OpenPar, ClosingPar, 
                       OpenPars + 1, N + 1)
                else if substr(St, N, length(ClosingPar)) == ClosingPar
                     then findClosing(St, OpenPar, ClosingPar, 
                            sd(OpenPars, 1), N + 1)
                     else findClosing(St, OpenPar, ClosingPar, 
                            OpenPars, N + 1)
                     fi
                fi
           fi
      fi .

  ---- finds the last opening unbalanced parenthesis 
  ---- findOpening("S{P1, P2{P21, P22}, P3}", "{", "}", 21) returns 1, not 8
  op findOpening : String String String Nat -> FindResult .
  op findOpening : String String String Nat Nat -> FindResult .

  eq findOpening(St, OpenPar, ClosingPar, N)
    = findOpening(St, OpenPar, ClosingPar, 0, N) .

  eq findOpening(St, OpenPar, ClosingPar, ClosingPars, N)
    = if N == 0
      then notFound
      else if ClosingPars == 0 
              and-then substr(St, N, length(ClosingPar)) == OpenPar
           then N
           else if substr(St, N, length(OpenPar)) == ClosingPar
                then findOpening(St, OpenPar, ClosingPar, 
                       ClosingPars + 1, sd(N, 1))
                else if substr(St, N, length(ClosingPar)) == OpenPar
                     then findOpening(St, OpenPar, ClosingPar, 
                            sd(ClosingPars, 1), sd(N, 1))
                     else findOpening(St, OpenPar, ClosingPar, 
                            ClosingPars, sd(N, 1))
                     fi
                fi
           fi
      fi .


  op getSort : Kind -> Sort .
  eq getSort(K:Kind) 
    = if findOut(string(K:Kind), "`,", "{", "}", 0) == notFound
      then qid(substr(string(K:Kind), 
                      2, 
                      sd(length(string(K:Kind)), 4)))
      else qid(substr(string(K:Kind), 
                      2, 
                      sd(findOut(string(K:Kind), "`,", "{", "}", 0), 2)))
      fi .


  op viewExp2Qid : ViewExp -> Qid .
  op parameterList2Qid : ParameterList -> Qid .
  op viewExp2QidList : ViewExp -> QidList .
  op parameterList2QidList : ParameterList -> QidList .

  var  P : ViewExp .
  var  PL : NeParameterList .

  eq parameterList2QidList(P) = viewExp2QidList(P) .
  ceq parameterList2QidList((P, PL))
    = (if QI == '`) then QIL QI  else QIL QI fi) 
      '`, parameterList2QidList(PL)
    if QIL QI := viewExp2QidList(P).

  eq viewExp2QidList(V{PL})
    = (viewExp2QidList(V) '`{ parameterList2QidList(PL) '`}) .
  ceq viewExp2QidList(VE ;; VE')
    = (viewExp2QidList(VE) '; viewExp2QidList(VE')) 
    if VE =/= mtViewExp /\ VE' =/= mtViewExp .
  eq viewExp2QidList(V) = V .
 
  eq parameterList2Qid(P) = viewExp2Qid(P) .
  eq parameterList2Qid((P, PL))
    = qid(string(viewExp2Qid(P)) + ", " + string(parameterList2Qid(PL))) .

  eq viewExp2Qid(VE) = qidList2Qid(viewExp2QidList(VE)) .


  op eMetaPrettyPrint : Sort -> QidList .
  op eMetaPrettyPrint : SortSet -> QidList .
  op eMetaPrettyPrint : TypeList -> QidList .
  op eMetaPrettyPrint : SubsortDeclSet -> QidList .
  op eMetaPrettyPrint : Module OpDeclSet -> QidList .
  op eMetaPrettyPrintVars : OpDeclSet -> QidList .
  op eMetaPrettyPrint : Module MembAxSet -> QidList .
  op eMetaPrettyPrint : Module EquationSet -> QidList .
  op eMetaPrettyPrint : Module RuleSet -> QidList .
  op eMetaPrettyPrint : Module Condition -> QidList .
  op eMetaPrettyPrint : Module Term -> QidList .

  eq eMetaPrettyPrint(U, MAS) = eMetaPrettyPrint(U, MAS, false) .
  eq eMetaPrettyPrint(U, EqS) = eMetaPrettyPrint(U, EqS, false) .
  eq eMetaPrettyPrint(U, RlS) = eMetaPrettyPrint(U, RlS, false) .
  eq eMetaPrettyPrint(U, Cond) = eMetaPrettyPrint(U, Cond, false) .
  eq eMetaPrettyPrint(U, T) = eMetaPrettyPrint(U, T, false) .

  op eMetaPrettyPrint : Module MembAxSet Bool -> QidList .
  op eMetaPrettyPrint : Module EquationSet Bool -> QidList .
  op eMetaPrettyPrint : Module RuleSet Bool -> QidList .
  op eMetaPrettyPrint : Module Condition Bool -> QidList .
  op eMetaPrettyPrint : Module Term Bool -> QidList .

  ---- error handling
  eq metaPrettyPrint(M, T, POS:PrintOptionSet) 
    = 'Module getName(M) 'contains 'errors. .

  eq eMetaPrettyPrint(U, T, false) = metaPrettyPrint(U, T,  mixfix flat 
format number rat) .
  eq eMetaPrettyPrint(U, T, true) = metaPrettyPrint(U, T) .
 
  op eMetaPrettyPrint : Module AttrSet -> QidList .
  op eMetaPrettyPrint : NatList -> QidList .
  op eMetaPrettyPrint : ViewExp -> QidList .
  op eMetaPrettyPrint : Module HookList -> QidList .

  vars QI QI' QI'' F V L : Qid .
  var  QIL : QidList .
  var  M : Module .
  var  U : Module .
  vars VE VE' : ViewExp .
  vars SS : SortSet .
  vars S S' : Sort .
  var  TyL : TypeList .
  var  Ty : Type .
  var  SSDS : SubsortDeclSet .
  var  OPDS : OpDeclSet .
  var  AtS : AttrSet .
  var  MAS : MembAxSet .
  var  EqS : EquationSet .
  var  RlS : RuleSet .
  var  Hk : Hook .
  var  HkL : HookList .
  var  I : Int .
  var  NL : NatList .
  vars T T' T'' T3 : Term .
  var  Cond : Condition .
  var  K : Kind .
  var B : Bool .

---  ceq eMetaPrettyPrint(VE) = viewExp2QidList(VE) if not VE :: TypeList .

  --- eq eMetaPrettyPrint(Ty) = Ty .

  eq eMetaPrettyPrint(S)
    = if getPars(S) == empty
      then S
      else getName(S) '`{ parameterList2QidList(getPars(S)) '`}
      fi .
  eq eMetaPrettyPrint(K) = '`[ eMetaPrettyPrint(getSort(K)) '`] . 

  eq eMetaPrettyPrint((S ; SS)) 
    = (eMetaPrettyPrint(S) eMetaPrettyPrint(SS))
    [owise] .
  eq eMetaPrettyPrint((none).SortSet) = nil .

  eq eMetaPrettyPrint(Ty TyL) 
    = eMetaPrettyPrint(Ty) eMetaPrettyPrint(TyL) 
    [owise] .
  eq eMetaPrettyPrint((nil).TypeList) = nil .

  eq eMetaPrettyPrint(((subsort S < S' .) SSDS))
    = ( 
       'subsort eMetaPrettyPrint(S) 
           '< eMetaPrettyPrint(S') '.  '\n
       eMetaPrettyPrint(SSDS)) .
  eq eMetaPrettyPrint((none).SubsortDeclSet) = nil .

  eq eMetaPrettyPrint(M, ((op F : TyL -> Ty [none] .) OPDS))
    = ( 
       'op F ': eMetaPrettyPrint(TyL) 
       '-> eMetaPrettyPrint(Ty) '.  '\n
       eMetaPrettyPrint(M, OPDS)) .
  eq eMetaPrettyPrint(M, ((op F : TyL -> Ty [AtS] .) OPDS))
    = ( 
       'op F ': eMetaPrettyPrint(TyL) 
       '-> eMetaPrettyPrint(Ty) 
         '`[ eMetaPrettyPrint(M, AtS) '`]  '. '\n
       
       eMetaPrettyPrint(M, OPDS))
    [owise] .
  eq eMetaPrettyPrint(M, (none).OpDeclSet)  = nil .

  eq eMetaPrettyPrintVars((op F : nil -> Ty [none] .) OPDS)
    = ( 'var F ': eMetaPrettyPrint(Ty) '. 
       eMetaPrettyPrintVars(OPDS)) .
  eq eMetaPrettyPrintVars((none).OpDeclSet)  = nil .

  eq eMetaPrettyPrint(M, (mb T : S [none] .) MAS, B)
    = ( 'mb eMetaPrettyPrint(M, T, B) 
                   ': eMetaPrettyPrint(S) '.  '\n
       eMetaPrettyPrint(M, MAS, B)) .
  eq eMetaPrettyPrint(M, (mb T : S [AtS] .) MAS, B)
    = ( 'mb eMetaPrettyPrint(M, T, B) 
                   ': eMetaPrettyPrint(S) 
                '`[ eMetaPrettyPrint(M, AtS) '`]  '.  '\n
       eMetaPrettyPrint(M, MAS, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, (cmb T : S if Cond [none] .) MAS, B)
    = ( 'cmb eMetaPrettyPrint(M, T, B) 
                   ': eMetaPrettyPrint(S) 
         'if eMetaPrettyPrint(M, Cond, B) '.  '\n
       eMetaPrettyPrint(M, MAS, B)) .
  eq eMetaPrettyPrint(M, (cmb T : S if Cond [AtS] .) MAS, B)
    = ( 'cmb eMetaPrettyPrint(M, T, B) 
                   ': eMetaPrettyPrint(S) 
         'if eMetaPrettyPrint(M, Cond, B) 
            '`[ eMetaPrettyPrint(M, AtS) '`]  '. '\n 
       eMetaPrettyPrint(M, MAS, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, (none).MembAxSet, B) = nil .

  eq eMetaPrettyPrint(M, ((eq T = T' [none] .) EqS), B)
    = ( 'eq  eMetaPrettyPrint(M, T, B) 
         '=  eMetaPrettyPrint(M, T', B)  '.  '\n
       
       eMetaPrettyPrint(M, EqS, B)) .
  eq eMetaPrettyPrint(M, ((eq T = T' [AtS] .) EqS), B)
    = ( 'eq  eMetaPrettyPrint(M, T, B) 
         '=  eMetaPrettyPrint(M, T', B) 
            '`[ eMetaPrettyPrint(M, AtS) '`]  '.  '\n
       eMetaPrettyPrint(M, EqS, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, ((ceq T = T' if Cond [none] .) EqS), B)
    = ( 'ceq  eMetaPrettyPrint(M, T, B) 
         '=  eMetaPrettyPrint(M, T', B) 
         'if  eMetaPrettyPrint(M, Cond, B)  '. '\n
       
       eMetaPrettyPrint(M, EqS, B)) .
  eq eMetaPrettyPrint(M, ((ceq T = T' if Cond [AtS] .) EqS), B)
    = ( 'ceq  eMetaPrettyPrint(M, T, B) 
         '=  eMetaPrettyPrint(M, T', B) 
         'if  eMetaPrettyPrint(M, Cond, B) 
            '`[ eMetaPrettyPrint(M, AtS) '`]  '.  '\n
       eMetaPrettyPrint(M, EqS, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, (none).EquationSet, B) = nil .

  eq eMetaPrettyPrint(M, ((rl T => T' [none] .) RlS), B)
    = ( 'rl  eMetaPrettyPrint(M, T, B) 
         '=>  eMetaPrettyPrint(M, T', B)  '. '\n
       
       eMetaPrettyPrint(M, RlS, B)) .
  eq eMetaPrettyPrint(M, ((rl T => T' [AtS] .) RlS), B)
    = ( 'rl  eMetaPrettyPrint(M, T, B) 
         '=>  eMetaPrettyPrint(M, T', B) 
            '`[ eMetaPrettyPrint(M, AtS) '`]  '. '\n
       eMetaPrettyPrint(M, RlS, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, ((crl T => T' if Cond [none] .) RlS), B)
    = ( 'crl  eMetaPrettyPrint(M, T, B) 
         '=>  eMetaPrettyPrint(M, T', B) 
         'if  eMetaPrettyPrint(M, Cond, B)  '. '\n
       
       eMetaPrettyPrint(M, RlS, B)) .
  eq eMetaPrettyPrint(M, ((crl T => T' if Cond [AtS] .) RlS), B)
    = ( 'crl  eMetaPrettyPrint(M, T, B) 
         '=>  eMetaPrettyPrint(M, T', B) 
         'if  eMetaPrettyPrint(M, Cond, B) 
            '`[ eMetaPrettyPrint(M, AtS) '`]  '.  '\n
       eMetaPrettyPrint(M, RlS, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, (none).RuleSet, B) = nil .

  eq eMetaPrettyPrint(M, T = 'true.Bool /\ Cond, B)
   =  (eMetaPrettyPrint(M, T, B)
      '/\ eMetaPrettyPrint(M, Cond, B)) 
    [owise] .
  ceq eMetaPrettyPrint(M, T = T' /\ Cond, B)
    = (eMetaPrettyPrint(M, T, B) '= eMetaPrettyPrint(M, T', B) 
      '/\ eMetaPrettyPrint(M, Cond, B)) if T' =/= 'true.Bool 
    [owise] .
  eq eMetaPrettyPrint(M, T : S /\ Cond, B)
    = (eMetaPrettyPrint(M, T, B) ': eMetaPrettyPrint(S) 
      '/\ eMetaPrettyPrint(M, Cond, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, T := T' /\ Cond, B)
    = (eMetaPrettyPrint(M, T, B) ':= eMetaPrettyPrint(M, T', B) 
      '/\ eMetaPrettyPrint(M, Cond, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, T => T' /\ Cond, B)
    = (eMetaPrettyPrint(M, T, B) '=> eMetaPrettyPrint(M, T', B) 
      '/\ eMetaPrettyPrint(M, Cond, B)) 
    [owise] .
  eq eMetaPrettyPrint(M, T = 'true.Bool, B) 
    = eMetaPrettyPrint(M, T, B) .
  eq eMetaPrettyPrint(M, T = T', B) 
    = (eMetaPrettyPrint(M, T, B) '= eMetaPrettyPrint(M, T', B)) [owise] .
  eq eMetaPrettyPrint(M, T : S, B) 
    = (eMetaPrettyPrint(M, T, B) ': eMetaPrettyPrint(S)) .
  eq eMetaPrettyPrint(M, T := T', B) 
    = (eMetaPrettyPrint(M, T, B) ':= eMetaPrettyPrint(M, T', B)) .
  eq eMetaPrettyPrint(M, T => T', B) 
    = (eMetaPrettyPrint(M, T, B) '=> eMetaPrettyPrint(M, T', B)) .

  eq eMetaPrettyPrint(M, (assoc AtS))  
    = ('assoc eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (comm AtS))   
    = ('comm eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (memo AtS))   
    = ('memo eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (idem AtS))   
    = ('idem eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (id(T) AtS))
    = ('id: eMetaPrettyPrint(M, T) eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (right-id(T) AtS))
    = ('right 'id: eMetaPrettyPrint(M, T) eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (left-id(T) AtS))
    = ('left 'id: eMetaPrettyPrint(M, T) eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (poly(NL) AtS))
    = ('poly '`( eMetaPrettyPrint(NL) '`) 
       eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (strat(NL) AtS))
    = ('strat '`( eMetaPrettyPrint(NL) '`) 
       eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (memo AtS))   
    = ('memo eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (prec(I) AtS))
    = ('prec eMetaPrettyPrint(I) eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (gather(QIL) AtS))
    = ('gather '`( QIL '`) eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (format(QIL) AtS))
    = ('format '`( QIL '`) eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (ctor AtS))   
    = ('ctor eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (frozen(NL) AtS)) 
    = ('frozen '`( eMetaPrettyPrint(NL) '`) 
       eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (iter AtS))   
    = ('iter eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (special(HkL) AtS))
    = ('special '`( eMetaPrettyPrint(M, HkL) '`) 
       eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (config AtS))   
    = ('config eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (object AtS))   
    = ('object eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (msg AtS))   
    = ('msg eMetaPrettyPrint(M, AtS)) .

  eq eMetaPrettyPrint(M, (label(QI) AtS))
    = ('label QI eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (metadata(St) AtS))
    = ('metadata qid("\"" + St + "\"") 
       eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (nonexec AtS))
    = ('nonexec eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (owise AtS))
    = ('owise eMetaPrettyPrint(M, AtS)) .
  eq eMetaPrettyPrint(M, (print(QIL) AtS))
    = ('print QIL eMetaPrettyPrint(M, AtS)) .

  eq eMetaPrettyPrint(M, (none).AttrSet) = nil .

  ceq eMetaPrettyPrint(M, (Hk HkL)) 
    = (eMetaPrettyPrint(M, Hk) eMetaPrettyPrint(M, HkL)) 
    if HkL =/= nil .
  eq eMetaPrettyPrint(M, id-hook(QI, nil)) = ('id-hook QI) .
  eq eMetaPrettyPrint(M, id-hook(QI, QIL))
    = ('id-hook QI '`( QIL '`) ) 
    [owise] .
  eq eMetaPrettyPrint(M, op-hook(QI, QI', nil, QI''))
    = ('op-hook QI '`( QI' ': '~> QI'' '`) ) .
  eq eMetaPrettyPrint(M, op-hook(QI, QI', QIL, QI''))
    = ('op-hook QI '`( QI' ': QIL '~> QI'' '`) ) 
    [owise] .
  eq eMetaPrettyPrint(M, term-hook(QI, T))
    = ('term-hook QI '`( eMetaPrettyPrint(M, T) '`) ) .

  eq eMetaPrettyPrint((I NL)) = (qid(string(I, 10)) eMetaPrettyPrint(NL)) .
  eq eMetaPrettyPrint((nil).NatList) = nil .

endfm

fmod MODULE-META-PRETTY-PRINT is
  pr META-LEVEL .
  ---pr RENAMING-EXPR-EVALUATION .
  pr DECL-META-PRETTY-PRINT .
  pr MOD-NAME-TO-QID .

  op eMetaPrettyPrint : Module Module -> QidList .

  op eMetaPrettyPrint : Module Module -> QidList .
  op eMetaPrettyPrint : Header -> QidList .
  op eMetaPrettyPrint : ParameterDeclList -> QidList .
  op eMetaPrettyPrint : ImportList -> QidList .

  var  M : Module .
  vars QI F F' L L' : Qid .
  var  QIL : QidList .
  var  ME : ModuleExpression .
  vars S S' : Sort .
  var  Ty : Type .
  var  TyL : TypeList .
  var  SS : SortSet .
  var  PD : ParameterDecl .
  var  PDL : ParameterDeclList .
  vars IL IL' : ImportList .
  var  SSDS : SubsortDeclSet .
  var  OPDS : OpDeclSet .
  var  MAS : MembAxSet .
  var  EqS : EquationSet .
  var  RlS : RuleSet .
  var  U : Module .
  var  AtS : AttrSet .
---  var  MN : ModuleName .

  ceq eMetaPrettyPrint(ME) 
    = if QI == '`) or QI == '`] or QI == '`} 
      then QIL QI 
      else QIL QI 
      fi
    if QIL QI := header2QidList(ME) .

---  eq eMetaPrettyPrint(W:[Module], unitError(QIL)) = QIL .
---  eq eMetaPrettyPrint(unitError(QIL), noModule) = QIL .
---  eq eMetaPrettyPrint(noModule, noModule) = nil .
  eq eMetaPrettyPrint(M, mod ME is IL sorts SS . SSDS OPDS MAS EqS RlS endm)
    = ('\n '---K-MAUDE-GENERATED-OUTPUT-BEGIN--- '\n          
       'mod eMetaPrettyPrint(ME) 'is 
          eMetaPrettyPrint(IL) 
          (if SS == none
           then nil
           else ( 'sorts eMetaPrettyPrint(SS) '. )
           fi)
          eMetaPrettyPrint(SSDS) 
          eMetaPrettyPrint(M, OPDS)
          eMetaPrettyPrint(M, MAS) 
          eMetaPrettyPrint(M, EqS) 
          eMetaPrettyPrint(M, RlS) 
        'endm '\n '---K-MAUDE-GENERATED-OUTPUT-END----- '\n) .
  eq eMetaPrettyPrint(M, mod ME{PDL} is IL sorts SS . SSDS OPDS MAS EqS RlS endm)
    = ( 
       'mod eMetaPrettyPrint(ME) (if PDL == nil 
                                      then nil
                                      else '`{ eMetaPrettyPrint(PDL) '`} 
                                      fi) 'is 
          eMetaPrettyPrint(IL) 
          (if SS == none
           then nil
           else ( 'sorts eMetaPrettyPrint(SS) '. )
           fi) 
          eMetaPrettyPrint(SSDS) 
          eMetaPrettyPrint(M, OPDS) 
          eMetaPrettyPrint(M, MAS) 
          eMetaPrettyPrint(M, EqS) 
          eMetaPrettyPrint(M, RlS) 
        'endm '\n) .
---(
  eq eMetaPrettyPrint(M, th MN is IL sorts SS . SSDS OPDS MAS EqS RlS endth)
    = ( 
       'th eMetaPrettyPrint(MN) 'is 
          eMetaPrettyPrint(IL) 
          (if SS == none
           then nil
           else ( 'sorts eMetaPrettyPrint(SS) '. )
           fi) 
          eMetaPrettyPrint(SSDS) 
          eMetaPrettyPrint(M, OPDS)
          eMetaPrettyPrint(M, MAS) 
          eMetaPrettyPrint(M, EqS) 
          eMetaPrettyPrint(M, RlS) 
        'endth '\n) .
---)
  eq eMetaPrettyPrint(M, fmod ME is IL sorts SS . SSDS OPDS MAS EqS endfm)
    = ( 
       'fmod eMetaPrettyPrint(ME) 'is 
           eMetaPrettyPrint(IL) 
           (if SS == none
            then nil
            else ( 'sorts eMetaPrettyPrint(SS) '. )
            fi)  
           eMetaPrettyPrint(SSDS) 
           eMetaPrettyPrint(M, OPDS)
           eMetaPrettyPrint(M, MAS) 
           eMetaPrettyPrint(M, EqS) 
        'endfm '\n) .
  eq eMetaPrettyPrint(M, fmod ME{PDL} is IL sorts SS . SSDS OPDS MAS EqS endfm)
    = ( 
       'fmod eMetaPrettyPrint(ME) (if PDL == nil 
                                       then nil
                                       else '`{ eMetaPrettyPrint(PDL) '`} 
                                       fi) 'is 
           eMetaPrettyPrint(IL) 
           (if SS == none
            then nil
            else ( 'sorts eMetaPrettyPrint(SS) '. )
            fi)  
           eMetaPrettyPrint(SSDS)          
           eMetaPrettyPrint(M, OPDS)
           eMetaPrettyPrint(M, MAS)          
           eMetaPrettyPrint(M, EqS) 
        'endfm '\n) .
---(
  eq eMetaPrettyPrint(M, fth MN is IL sorts SS . SSDS OPDS MAS EqS endfth)
    = ( 
       'fth eMetaPrettyPrint(MN) 'is 
          eMetaPrettyPrint(IL)
          (if SS == none
           then nil
           else ( 'sorts eMetaPrettyPrint(SS) '. )
           fi)  
          eMetaPrettyPrint(SSDS) 
          eMetaPrettyPrint(M, OPDS)
          eMetaPrettyPrint(M, MAS) 
          eMetaPrettyPrint(M, EqS) 
        'endfth '\n) .
---)

  eq eMetaPrettyPrint((including ME .) IL)
    = ( 'including eMetaPrettyPrint(ME) '.  '\n
       eMetaPrettyPrint(IL)) .
  eq eMetaPrettyPrint((extending ME .) IL)
    = ( 'extending eMetaPrettyPrint(ME) '.  '\n
       eMetaPrettyPrint(IL)) .
  eq eMetaPrettyPrint((protecting ME .) IL)
    = ( 
       'protecting eMetaPrettyPrint(ME) '. '\n eMetaPrettyPrint(IL)) .
  eq eMetaPrettyPrint((nil).ImportList) = nil .

  eq eMetaPrettyPrint((QI :: ME, PDL))
    = (QI '::  eMetaPrettyPrint(ME) '`, eMetaPrettyPrint(PDL))
    [owise] .
  eq eMetaPrettyPrint((QI :: ME)) = (QI '::  eMetaPrettyPrint(ME)) .
  eq eMetaPrettyPrint((nil).ParameterDeclList) = (nil).QidList .

  op eMetaPrettyPrint : ModuleExpression -> QidList .
  eq eMetaPrettyPrint(QI + ME:ModuleExpression)
    = QI '+ eMetaPrettyPrint(ME:ModuleExpression) .

endfm


