in ../../../k-prelude.maude
in simple-untyped-syntax.kmaude
in simple-untyped-programs.maude

kmod SIMPLE-UNTYPED-SEMANTICS is including SIMPLE-UNTYPED-DESUGARED-SYNTAX + K 
--- We need values when we define the semantics

  syntax Val ::= Int | Bool
  syntax Exp ::= Val  

--- We have to subsort all the syntactic categories to K, and the lists of values
--- to KResult.  Recall that the second argument of a function application is a list
--- of expressions, so that becomes a result when it becomes a list of values

  syntax K ::= Stmts | List{Exp} 
  syntax KResult ::= List{Val}

  configuration <T> 
                     <k> K:K </k>
                     <fstack> .List </fstack>
                     <env> .Map </env>
                     <genv> .Map </genv>
                     <store> .Map </store>
                     <in> .List </in>
                     <out> .List </out>
                     <nextLoc> 0 </nextLoc>
                </T> 

  rule <k> X:Id => V:Val <_/k>  
       <env_> X |-> L:Nat <_/env>
       <store_> L |-> V <_/store> 
  rule <k> ++ X => I:Int +Int 1 <_/k>
       <env_> X |-> L <_/env>
       <store_> L |-> (I => I +Int 1) <_/store> 
  rule I1:Int + I2:Int => I1 +Int I2 
  rule _-_(I1,I2) => _-Int_(I1,I2) 
  rule I1 * I2 => I1 *Int I2 
  rule I1 / I2 => I1 /Int I2 if I2 =/=Bool 0 
  rule I1 % I2 => I1 %Int I2 if I2 =/=Bool 0 
  rule - I => -Int I 
  rule I1 < I2 => I1 <Int I2 
  rule I1 <= I2 => I1 <=Int I2 
  rule I1 > I2 => I1 >Int I2 
  rule I1 >= I2 => I1 >=Int I2 
  rule V1:Val == V2:Val => V1 ==Bool V2 
  rule V1 != V2 => V1 =/=Bool V2 
  rule T1:Bool and T2:Bool => T1 andBool T2 
  rule T1 or T2 => T1 orBool T2 
  rule not(T:Bool) => notBool(T) 

--- array value:
---    first arg. is the location of the first element in the array, and 
---    the second arg. is the array size
  syntax Val ::= array ( Nat , Nat )  

  rule <k> array(L,_)[N:Nat] => Sigma(L +Int N) <_/k> <store> Sigma </store> .
                                --- untyped SIMPLE does not check array bounds

  rule (sizeOf(array(_,N)) => N) 
  rule <k> var X ; => .K <_/k> 
       <env> Env:Map => Env[L / X] </env> 
       <store_> .Map => L |-> 0 <_/store>
       <nextLoc> L => sNat(L) </nextLoc> 
  
--- If an array is declared, then calculate its size; 
---    then next rule gives its semantics
  context var X[[HOLE]];   
  rule <k> var X[N] ; => .K <_/k> 
       <env> Env => Env[L / X] </env>
       <store_>.Map => L |-> array(sNat L, N) <_/store>
       <nextLoc> L => sNat L +Nat N </nextLoc> 

  rule {} => .K 
  rule <k> {Ss:Stmts} => Ss ~> env(Env) <_/k> <env> Env </env> 
  rule S1:Stmt S2:Stmt => S1 ~> S2 
  rule <k> X = V ; => .K <_/k> 
       <env_> X |-> L <_/env> 
       <store_> L |-> (_ => V) <_/store> 

 --- for array assignment, first evaluate the array and its index
  context _[[HOLE]] = _ ;  
  context [HOLE][_] = _ ; 
  rule <k> array(L,_)[N] = V ; => .K <_/k> 
       <store> Sigma:Map => Sigma[V / (L +Int N)] </store> 

  rule V ; => .K 
  rule if true then S:Stmt else _ => S 
  rule if false then _ else S => S 
  rule <k> while B:Exp do S  => if B then {S while B do S} else {} <_/k> 

--- function value; like arrays, functions also evaluate to 
---    their address (wrapped as a specific value)
  syntax Val ::= function( Nat )
---  op function : Nat -> Val   
  rule <k> function F:Id(Xl:List`{Id`}) S => .K <_/k> 
       <env> Env => Env[L / F] </env> 
       <nextLoc> L => L +Nat 2 </nextLoc> 
       <store_>(.Map => L |-> function(sNat L) sNat(L) |-> lambda(Xl,S))<_/store> 

--- actual function values are stored as lambda abstractions
  syntax Val ::= lambda ( List{Id} , Stmt )

--- the next is used to stack the environment and the computation at each function call
  syntax ListItem ::=  ( Map , K )

  rule <k> _`(_`)(function(L),Vl:List`{Val`})  ~> K => S ~> return(0); </k> 
       <fstack> .List => (Env,K) <_/fstack>
       <env> Env =>  GEnv:Map[| Xl | from N / getList{K}(Xl)] </env> 
       <genv> GEnv </genv>
       <store_> L |-> lambda(Xl,S) (.Map => | Xl | from N |-> getList{K}(Vl)) <_/store>
       <nextLoc> N => N +Nat | Xl | </nextLoc> 

  rule <k> return(V); ~> _ => V ~> K </k> 
       <fstack> (Env,K) => .List <_/fstack> 
       <env> _ => Env </env> 
  rule <k> read() => I <_/k>
       <in> ListItem(I) => .List <_/in> 
  rule <k> write(I) ; => .K <_/k>
       <out_> .List => ListItem(I) </out>  

--- when we are done with the first pass (computation and genv are empty),
---   call main and initialize the global environment
  rule <k> .K => main(); </k> <env> Env </env> <genv> .Map => Env </genv> 

----------------------------------------------
--- Auxilliary declarations and operations ---
----------------------------------------------
--- List{Val}
  syntax List{Val} ::= Val |  List{Val} , List{Val} [ditto]
  syntax List{Exp} ::= List{Val}

--- length
  syntax Nat ::= `| List{Id} `|
  macro | .List{Id} | = 0  
  macro | X,Xl | = sNat(| Xl |)

--- generate sequence of locations
---  op _from_ : Nat Nat -> List{K}  eq 0 from N1:Nat = .List{K}  eq sNat N2:Nat from N1 = N1,,(N2 from sNat N1) 

  syntax List{K} ::= Nat from Nat
  macro 0 from N1:Nat = .List{K}
  macro sNat N2:Nat from N1 = N1,,(N2 from sNat N1) 

--- environment recovery
  syntax K ::= env ( Map ) 
  rule env(_) ~> env(Env) => env(Env) [structural] 
  rule <k> env(Env) => .K <_/k> <env> _ => Env </env> [structural] 
  rule <k> V ~> (env(Env) => .K) <_/k> <env> _ => Env </env> [structural]

endkm

kmod SIMPLE-UNTYPED is including SIMPLE-UNTYPED-SEMANTICS + SIMPLE-UNTYPED-PROGRAMS 
--- make sure the name of the final module is the capitalized version of the file name

  syntax Bag ::= run ( KLabel )

--- After loading simple-untyped-compiled into maude, type "rew run .".
---     Replace pSorting by any other program, then recompile
  macro run(K:KLabel)
        =
        <T_> 
           <k> K(.List{K}) </k>
           <in>
              List(7,,23,,654,,656,,54,,456,,436345647643567,,
                   354654364356,,4357643576)
           </in>
        <_/T> 
endkm
