// This program tests multi-dimensional arrays.

class Matrix {
  var lines, columns, matrix;

  method Matrix(x,y) {
    lines = x;
    columns = y;
    var a[x,y];
    matrix = a;
  }

  method readMatrix() {
    for (var i=0; i<lines; ++i) {
      print("Line ", i+1, " (", columns, " elements): ");
      for (var j=0; j<columns; ++j) {
        matrix[i][j] = read();
      }
    }
  }

  method printMatrix() {
    for (var i=0; i<lines; ++i) {
      print("Line ",i+1," (",columns," elements):");
      for (var j=0; j<columns; ++j) {
        print(" ",matrix[i,j]);
      }
      print("\n");
    }
  }

  method copy() {
    var a = new Matrix(lines,columns);
    for (var i=0; i<lines; ++i) {
      for (var j=0; j<columns; ++j) {
        a.matrix[i,j] = matrix[i,j];
      }
    }
    return a;
  }

  method transpose() {
    var a[columns,lines];
    for (var i=0; i<lines; ++i) {
      for (var j=0; j<columns; ++j) {
        a[j,i] = matrix[i,j];
      }
    }
    var temp = lines;
    lines = columns;
    columns = temp;
    matrix = a;
  }

  method multiply(a) {
    if (a.lines != columns) {
      print("Incompatible dimensions.\n");
      return;
    }
    var b[lines,a.columns];
    for (var i=0; i<lines; ++i) {
      for (var j=0; j<a.columns; ++j) {
        b[i,j] = 0;
        for (var k=0; k<columns; ++k) {
          b[i,j] = b[i,j] + matrix[i,k] * a.matrix[k,j];
	}
      }
    }
    columns = a.columns;
    matrix = b;
  }
}


class Main {
  method Main() {
    print("Input the number of lines and columns (two natural numbers): ");
    var x = read(), y = read(), a = new Matrix(x,y);
    a.readMatrix();
    print("Your matrix is:\n");
    a.printMatrix();
    var b = a.copy();
    print("Here is a copy of your matrix:\n");
    b.printMatrix();
    print("The transpose of your matrix is:\n");
    b.transpose();
    b.printMatrix();
    print("You matrix multiplied with its transpose is:\n");
    var c = a.copy();
    c.multiply(b);
    c.printMatrix();
    print("The transpose of your matrix multiplied with your matrix is:\n");
    b.multiply(a);
    b.printMatrix();
    print("Done.\n");
  }
}

// Should output what it says.
