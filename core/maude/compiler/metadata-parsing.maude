fmod PARSE-METADATA-BASIC  is
  including CONVERSION .
  including META-MODULE .
  including STRING-EXTRAS .
  including META-ATTRS .

  sorts AttrStringPair .
  op {_,_} : AttrSet String -> AttrStringPair .

  var S : String .
  eq metadataParse(S) = metadataStrippedParse(stripSpaces(S)) .
  op metadataStrippedParse : String -> AttrSet .
  eq metadataStrippedParse("") = none .

  op metadataParse : MembAx -> MembAx .
  op metadataParse : OpDecl -> OpDecl .
  eq metadataParse(mb Trm : Sort [AS metadata(S) AS'] .) = (mb Trm : Sort [AS metadataParse(S) AS'] .) .
  eq metadataParse(op Q : TyL -> Ty [AS metadata(S) AS'] .) =  (op Q : TyL -> Ty [AS metadataParse(S) AS'] .).
  eq metadataParse(Max) = Max [owise] .
  eq metadataParse(Op) = Op [owise] .

 ceq metadataStrippedParse(S)
   = pair(substr(S, 0,N1),substr(S, N1 + 2, _-_(N2, N1 + 2))) 
     metadataParse(substr(S, N2 + 1, length(S)))
  if N1 := find(S, "=(",0) 
  /\ not(find(substr(S,0,N1), " ", 0) :: Nat)
  /\ N2 := find(S, ")", N1 + 2) .


  var Trm : Term . var Sort : Sort . var AS AS' : AttrSet . var TyL : TypeList . var Ty : Type . var N1 N2 : Nat .
  var Q : Qid .  var Max : MembAx . var Op : OpDecl .
endfm

fmod PARSE-METADATA-LOCATION is 
  including PARSE-METADATA-BASIC .
  including LOCATION-ATTRS .
  var S S' : String . var N N1 N2 : Nat . var AS AS' : AttrSet .
  eq pair("location",S) = klocation(S) .

  op getLocation : AttrSet -> String .
  eq getLocation(AS metadata(S) AS') = getLocation(AS metadataParse(S) AS') .
  eq getLocation(AS) = "" [owise print "could not find location for " AS] .

  op getLocation : OpDecl -> String .
  op getLocation : Equation -> String .
  op getLocation : MembAx -> String .
  op getLocation : Rule -> String .

  eq getLocation(op Q : Tyl -> Ty [AS] .) = getLocation(AS) .
  eq getLocation(eq Trm1 = Trm2 [AS] .) = getLocation(AS) .
  eq getLocation(rl Trm1 => Trm2 [AS] .) = getLocation(AS) .
  eq getLocation(ceq Trm1 = Trm2 if Cond [AS] .) = getLocation(AS) .
  eq getLocation(crl Trm1 => Trm2 if Cond [AS] .) = getLocation(AS) .
  eq getLocation(mb Trm : Ty [AS] .) = getLocation(AS) .
  eq getLocation(cmb Trm : Ty if Cond [AS] .) = getLocation(AS) .

  var Q : Qid . var Tyl : TypeList . var Ty : Type . 
  var Trm Trm1 Trm2 : Term .  var Cond : Condition .

  eq getLocation(AS klocation(S) AS') = S .
  eq getLocation(AS) = "" [owise] .

  op getBeginningLocation : String -> Int .
 ceq getBeginningLocation(S) = if rat(substr(S,N1, _-_(N2, N1)), 10) :: Nat then rat(substr(S,N1, _-_(N2, N1)), 10)  else -1 fi
  if N1 := if find(S,":",0) :: Nat then find(S, ":", 0) + 1 else 0 fi 
  /\  N2 := if find(S, "-", N1) :: Nat then  find(S, "-", N1) else if find(S, ":", N1) :: Nat then  find(S, ":", N1) else length(S) fi fi .

  op getOffsetLocation : String -> Int .
 ceq getOffsetLocation(S)
   = if rat(substr(S,N:[Nat] + 1, length(S)), 10) :: Nat then  rat(substr(S,N:[Nat] + 1, length(S)), 10) else 0 fi
   if N:[Nat] := find(S,":",find(S,":",0) + 1) .
endfm

fmod PARSE-METADATA-SYNTAX is
  including PARSE-METADATA-BASIC .
  including SYNTAX-ATTRS .
  var S S' S1 : String . var F : FindResult .  var Q : Qid .
  var N : Nat .  var AS AS' : AttrSet .
  eq pair("ditto","") = kditto .

 ceq metadataStrippedParse(S) = krenameTo(Q) metadataParse(S')
   if "renameTo " = substr(S,0,length("renameTo ")) 
   /\ S1 := stripSpaces(substr(S,length("renameTo "),length(S))) 
   /\ F :=   find(S1," ", 0)
   /\ Q := qid(if (F == notFound) then  S1 else substr(S1, 0,F) fi)
   /\ S' := if (F == notFound) then "" 
            else stripSpaces(substr(S1, find(S1," ",0),length(S1))) fi .
  ceq metadataStrippedParse(S) = kcolor(Q) metadataParse(S1) 
   if "color:" == substr(S,0,length("color:")) 
   /\ S' := stripSpaces(substr(S,length("color:"), length(S))) + " " 
   /\ N := find(S', " ", 0)
   /\ Q := qid(substr(S', 0, N))
   /\ S1 := substr(S', N, length(S')) .
   eq pair("latex",S) = klatex(metadataParse(S)) .
endfm

fmod PARSE-METADATA-STRICT is
  including PARSE-METADATA-BASIC .
  including STRICT-ATTRS .
  including K-META-OPS .
  var Q : Qid . var AS : AttrSet . var Nl : IntList .
  var A : Attr . var S S' S1 S2  S3 : String . var F : FindResult .
  var P P' P'' : Nat .  var T : Type . var N : Int . 

  eq pair("hybrid","") = khybrid .
  eq pair("strict",S) = kstrict(getStrictList(S),klabel) .
  eq pair("seqstrict",S) = seqkstrict(getStrictList(S),klabel) .
 ceq pair(S,S') = kstrict(getStrictList(S'),qid(substr(S,0,  _-_(length(S),length("-strict")))))
  if "-strict" = substr(S, _-_(length(S),length("-strict")), length(S)) .
 ceq pair(S,S') = kstrict(getStrictList(S'),qid(substr(S,0,  _-_(length(S),length("-seqstrict")))))
  if "-seqstrict" = substr(S, _-_(length(S),length("-seqstrict")), length(S)) .

  op getStrictList : String -> IntList .
  eq getStrictList(S) = parseIntList(tokenize(stripSpaces(S))) .

--- ceq metadataStrippedParse(S) = klist(N,Q,T) metadataParse(S')
---   if "list(" = substr(S,0,length("list(")) 
---   /\ S1 := stripSpaces(substr(S,length("list("),length(S))) 
---   /\ P :=   find(S1,")", 0)
---   /\ S2 := substr(S1, 0, P) 
---   /\ P' := find(S2,":",0)
---   /\ N := rat(stripSpaces(substr(S2,0, P')), 10)
---   /\ S3 := substr(S2,P' + 1, length(S2))
---   /\ P'' := rfind(S3,":",length(S3))
---   /\ Q := qid(stripSpaces(substr(S3,0,P'')))
---   /\ T := qid(stripSpaces(substr(S3,P'' + 1, length(S3))))
---   /\ S' := stripSpaces(substr(S1, P + 1,length(S1)))  .

 ceq metadataStrippedParse(S) = karity(N) metadataParse(S')
   if "arity " = substr(S,0,length("arity ")) 
   /\ S1 := stripSpaces(substr(S,length("arity "),length(S))) 
   /\ F :=   find(S1," ", 0)
   /\ N := rat(if (F == notFound) then  S1 else substr(S1, 0,F) fi,10)
   /\ S' := if (F == notFound) then "" 
            else stripSpaces(substr(S1, find(S1," ",0),length(S1))) fi .

endfm

fmod PARSE-METADATA-CONFIGURATION is
  including PARSE-METADATA-BASIC .
  including CONFIGURATION-ATTRS .
  var S S' S1 : String . var F : FindResult .  var Q : Qid .  var N : Nat .
  ceq metadataStrippedParse(S) = kwrapping(Q) metadataParse(S1) 
   if "wrapping " == substr(S,0,length("wrapping ")) 
   /\ S' := stripSpaces(substr(S,length("wrapping "), length(S))) + " " 
   /\ N := find(S', " ", 0)
   /\ Q := qid(substr(S', 0, N))
   /\ S1 := substr(S', N, length(S')) .
endfm

fmod PARSE-METADATA-RULES is
  including PARSE-METADATA-BASIC .
  including RULES-ATTRS .
  eq pair("large","") = klarge .
endfm

fmod PARSE-METADATA is
  including PARSE-METADATA-STRICT .
  including PARSE-METADATA-CONFIGURATION .
  including PARSE-METADATA-SYNTAX .
  including PARSE-METADATA-RULES .
  including PARSE-METADATA-LOCATION .
  including META-K-ERROR .
  eq metadataStrippedParse(S) 
   = errorAS(S, "Could not parse attributes") [owise print "Could not parse metadata: " S] .
 
  var S : String .
endfm
---(
red metadataParse("") .
red metadataParse("strict") .
red metadataParse("strict(1)") .
red metadataParse("strict(1 3 2)") .
red metadataParse("  strict   (  1  3  2   )   ") .
red metadataParse("strict(1 3 2)") .
red metadataParse("strict(ala bala portocala)") .
red metadataParse("aux") .
red metadataParse("strict seqstrict(1) in (state) strict(1 3 2) seqstrict  seqstrict   (  1  3  2   )   ") .
red metadataParse("strict seqstrict(1)   seqstrict   (  1  3  2   )   ") .
red metadataParse("disolve renameTo _ana") .
red metadataParse("strict disolve renameTo _are") .
red metadataParse("renameTo mere_ strict(1) disolve") .
red metadataParse("  strict   (  1  3  2   )   ") .
red metadataParse("strict renameTo _gina_ disolve seqstrict(1) strict(1 3 2) seqstrict  seqstrict   (  1  3  2   )   ") .
red metadataParse("strict seqstrict(1) renameTo _are_   seqstrict   (  1  3  2   )   ") .
red metadataParse("strict seqstrict(1) renameTo _are_   disolve seqstrict   (  1  3  2   )   ") .
red metadataParse("strict seqstrict(1) arity 5 renameTo _are_  arity 9 disolve wrapping K seqstrict   (  1  3  2   )   ") .
red metadataParse("wrapping Set`{ConfigItem`}") .
red metadataParse("latex ( color: red wrapping Me)") .
red metadataParse("arity 1 list(1:_`,_:K) strict(1) hybrid") .
q
---)

