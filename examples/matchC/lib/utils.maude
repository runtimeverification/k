load meta
load smt

mod CONST-UTILS is inc MATCHC .
  op errorPattern : -> [Pattern] .
  op errorBag : -> [Bag] .
  op errorFormula : -> [Formula] .
  op errorSubst : -> [Subst] .
endm


mod PATTERN-SKOLEM is
inc MATCHC
  + PL-META-TERM
  + CONST-UTILS
  + MAUDE-IF-THEN-ELSE .

  op skolemTerm : Term TermList -> TermList .
  op co-skolemTerm : Term TermList -> TermList .

  var Q : Qid .
  var QC : Constant .
  var QT NT : Term .
  var QTList : TermList .
  var QTNeList : NeTermList .


  eq skolemTerm(NT, (QT : QTNeList))
   = skolemTerm(NT, QT) : skolemTerm(NT, QTNeList) .
  eq skolemTerm(NT, .emptyTermList) = .emptyTermList .
  eq skolemTerm(NT, QC)
   = if substrString(string(getType(QC)), 0, 2) ==Bool "PE" then
       'skolem[NT : QC]
     else
       QC
     fi .
 ceq skolemTerm(NT, Q[QTList]) = 'skolem[NT : Q[QTList]]
  if substrString(string(Q), 0, 1) ==Bool "?" .
 ceq skolemTerm(NT, Q[QTList]) = Q[QTList]
  if Q ==Bool 'skolem .
 ceq skolemTerm(NT, '_`(_`)[QC : QTList]) = '_`(_`)[QC : QTList]
  if getName(QC) ==Bool ''@`assume_ orBool
     getName(QC) ==Bool ''@`assert_ orBool
     getName(QC) ==Bool ''@`invariant_ orBool
     getName(QC) ==Bool ''@`pre_ orBool
     getName(QC) ==Bool ''@`post_ .
 ceq skolemTerm(NT, '<_>_</_>[QC : QT : QC]) = '<_>_</_>[QC : QT : QC]
  if getName(QC) ==Bool 'struct orBool
     getName(QC) ==Bool 'fun orBool
     getName(QC) ==Bool 'tenv .
  eq skolemTerm(NT, Q[QTList]) = Q[skolemTerm(NT, QTList)] [owise] .

 
  eq co-skolemTerm(NT, (QT : QTNeList))
   = co-skolemTerm(NT, QT) : co-skolemTerm(NT, QTNeList) .
  eq co-skolemTerm(NT, .emptyTermList) = .emptyTermList .
  eq co-skolemTerm(NT, QC)
   = if substrString(string(getType(QC)), 0, 4) ==Bool "Free" then
       'co-skolem[NT : QC]
     else
       QC
     fi .
  eq co-skolemTerm(NT, Q[QTList])
   = if substrString(string(Q), 0, 4) ==Bool "Free" orBool Q ==Bool 'skolem then
       'co-skolem[NT : Q[QTList]]
     else
       if Q =/=Bool 'co-skolem then
         Q[co-skolemTerm(NT, QTList)]
       else
         Q[QTList]
       fi
     fi .
 

  var N : Nat .
  var Pattern : Pattern .

  eq skolemize(N, Pattern)
   = downTerm(skolemTerm(upTerm(N), upTerm(Pattern)), errorPattern) .
  eq co-skolemize(N, Pattern)
   = downTerm(co-skolemTerm(upTerm(N), upTerm(Pattern)), errorPattern) .
endm


mod APPLY-SUBST is
inc MATCHC
  + PL-META-TERM
  + CONST-UTILS
  + MAUDE-IF-THEN-ELSE .

  sort MetaSubst .
  op .MetaSubst : -> MetaSubst .
  op _;_ : MetaSubst MetaSubst -> MetaSubst [assoc comm id: .MetaSubst] .
  op _<-_ : Term Term -> MetaSubst .
  op upSubst : Subst -> MetaSubst .
  op upSubstItem : SubstItem -> MetaSubst .

  var K1 K2 : K .
  var List1 List2 : List .
  var Bag1 Bag2 : Bag .
  var Set1 Set2 : Set .
  var Map1 Map2 : Map .
  var Subst : Subst .
  var SubstItem : SubstItem .

  eq upSubst(SubstItem ;; Subst) = upSubstItem(SubstItem) ; upSubst(Subst) .
  eq upSubst(.Subst) = .MetaSubst .
  eq upSubstItem(K1 <- K2) = upTerm(K1) <- upTerm(K2) .
  eq upSubstItem(List1 <- List2) = upTerm(List1) <- upTerm(List2) .
  eq upSubstItem(Bag1 <- Bag2) = upTerm(Bag1) <- upTerm(Bag2) .
  eq upSubstItem(Set1 <- Set2) = upTerm(Set1) <- upTerm(Set2) .
  eq upSubstItem(Map1 <- Map2) = upTerm(Map1) <- upTerm(Map2) .


  op substTerm : TermList MetaSubst -> TermList .

  var Q : Qid .
  var QC : Constant .
  var QT QT1 QT2 : Term .
  var QTList : TermList .
  var QTNeList : NeTermList .
  var MetaSubst : MetaSubst .

  eq substTerm((QT : QTNeList), MetaSubst)
   = substTerm(QT, MetaSubst) : substTerm(QTNeList, MetaSubst) .
  eq substTerm(.emptyTermList, MetaSubst) = .emptyTermList .
  eq substTerm(QT1, ((QT1 <- QT2) ; MetaSubst)) = QT2 .
 ceq substTerm('_`(_`)[QC : QTList], MetaSubst) = '_`(_`)[QC : QTList]
  if getName(QC) ==Bool ''@`assume_ orBool
     getName(QC) ==Bool ''@`assert_ orBool
     getName(QC) ==Bool ''@`invariant_ orBool
     getName(QC) ==Bool ''@`pre_ orBool
     getName(QC) ==Bool ''@`post_ .
 ceq substTerm(Q[QTList], MetaSubst) = Q[QTList]
  if Q ==Bool 'skolem orBool Q ==Bool 'co-skolem [owise] .
  eq substTerm(QC, MetaSubst) = QC [owise] .
  eq substTerm(Q[QTList], MetaSubst)
   = Q[substTerm(QTList, MetaSubst)] [owise] .


  var Pattern Pattern1 Pattern2 : Pattern .
  var CellLabel : CellLabel .
  var K : K .
  var List : List .
  var Bag : Bag .
  var Map : Map .
  var Phi : Formula .
  var Subst1 Subst2 : Subst .

  --- apply default substitution on a pattern
  eq applyDefaultSubst(Pattern1 \/ Pattern2, Subst)
   = applyDefaultSubst(Pattern1, Subst) \/ applyDefaultSubst(Pattern2, Subst) .
  eq applyDefaultSubst(Bag /\ Phi, Subst)
   = applyDefaultSubst(Bag, Subst) /\ Phi .

  --- apply default substitution on a configuration
  eq applyDefaultSubst(< CellLabel > Map1 Map </ CellLabel > Bag,
                       (Map1 <- Map2) ;; Subst)
   = < CellLabel > Map2 Map </ CellLabel > applyDefaultSubst(Bag, Subst) .
  eq applyDefaultSubst(< CellLabel > K1 </ CellLabel > Bag, (K1 <- K2) ;; Subst)
   = < CellLabel > K2 </ CellLabel > applyDefaultSubst(Bag, Subst) .
  eq applyDefaultSubst(< CellLabel > Bag1 </ CellLabel > Bag2, Subst)
   = < CellLabel > applyDefaultSubst(Bag1, Subst) </ CellLabel >
     applyDefaultSubst(Bag2, Subst) [owise] .
  eq applyDefaultSubst(Bag, Subst) = Bag [owise] .

  --- apply substitution on a pattern
  eq applySubst(Pattern1 \/ Pattern2, Subst)
   = applySubst(Pattern1, Subst) \/ applySubst(Pattern2, Subst) .
  eq applySubst(Bag /\ Phi, Subst)
   = applySubst(Bag, Subst) /\ applySubst(Phi, Subst) .

  --- apply substitution on a configuration
 ceq applySubst(< CellLabel > Map </ CellLabel > Bag, Subst)
   = < CellLabel > Map </ CellLabel > applySubst(Bag, Subst)
  if CellLabel ==Bool struct orBool
     CellLabel ==Bool fun orBool
     CellLabel ==Bool tenv .
 ceq applySubst(< CellLabel > K </ CellLabel > Bag, Subst)
   = < CellLabel > K </ CellLabel > applySubst(Bag, Subst)
  if CellLabel ==Bool taskType orBool
     CellLabel ==Bool program orBool
     CellLabel ==Bool counter .
  eq applySubst(< CellLabel > Bag1 </ CellLabel > Bag2, Subst)
   = < CellLabel > applySubst(Bag1, Subst) </ CellLabel >
     applySubst(Bag2, Subst) [owise] .
  eq applySubst(Bag, Subst)
   = downTerm(substTerm(upTerm(Bag), upSubst(Subst)), errorBag) [owise] .

  --- apply substitution on a formula
  eq applySubst(Phi, Subst)
   = downTerm(substTerm(upTerm(Phi), upSubst(Subst)), errorFormula) .

  --- apply substitution on a substitution
  eq applySubst(Subst1, Subst2)
   = downTerm(substTerm(upTerm(Subst1), upSubst(Subst2)), errorSubst) .
endm


mod UTILS is inc PATTERN-SKOLEM + APPLY-SUBST + SMT-LIB + SMT-INTERFACE .
  var I : Int++ .
  eq isInt(I) = I :: Int .

  var Obj : MathObj++ .
  var PEObj : PEMathObj .
  var FEObj : FEMathObj .
  var FreeObj : FreeMathObj .
  var Phi : Formula .

  eq getPESubstItem(PEObj === Obj /\ Phi) = (PEObj <- Obj) .
  eq getPESubstItem(Obj === PEObj /\ Phi) = (PEObj <- Obj) .
  eq getPESubstItem(Phi) = .Subst [owise] .

  eq getFESubstItem(FEObj === Obj /\ Phi) = (FEObj <- Obj) .
  eq getFESubstItem(Obj === FEObj /\ Phi) = (FEObj <- Obj) .
  eq getFESubstItem(Phi) = .Subst [owise] .

  eq getFreeSubstItem(FreeObj === Obj /\ Phi) = (FreeObj <- Obj) .
  eq getFreeSubstItem(Obj === FreeObj /\ Phi) = (FreeObj <- Obj) .
  eq getFreeSubstItem(Phi) = .Subst [owise] .

  var Obj1 Obj2 Obj1' Obj2' : MathObj++ .
  var ObjList ObjList' : List{MathObj++} .
  eq (Obj1, Obj2, ObjList) <- (Obj1', Obj2', ObjList')
   = (Obj1 <- Obj1') ;; (Obj2 <- Obj2') ;; (ObjList <- ObjList') .

  op filterPrePhiDefault : NtFormula TruthFormula -> Formula .
  var T : TruthFormula .
  var A : AtomFormula .
  var NtPhi NtPhi1 NtPhi2 : NtFormula .
  eq filterPrePhi(T) = T .
  eq filterPrePhi(NtPhi) = filterPrePhiDefault(NtPhi, TrueFormula) .
  eq filterPrePhiDefault(NtPhi1 /\ NtPhi2, T)
   = filterPrePhiDefault(NtPhi1, TrueFormula)
     /\ filterPrePhiDefault(NtPhi2, TrueFormula) .
  eq filterPrePhiDefault(~ NtPhi, T) = ~ filterPrePhiDefault(NtPhi, ~ T) .
  eq filterPrePhiDefault(NtPhi1 \/ NtPhi2, T)
   = filterPrePhiDefault(NtPhi1, FalseFormula)
     \/ filterPrePhiDefault(NtPhi2, FalseFormula) .
  ceq filterPrePhiDefault(A, T) = A
   if isLhsTerm(upTerm(A)) .
  ceq filterPrePhiDefault(A, T) = T
   if notBool isLhsTerm(upTerm(A)) .

  op isLhsTerm : TermList -> Bool . 
  var Q : Qid .
  var QC : Constant .
  var QT : Term .
  var QTList : TermList .
  var QTNeList : NeTermList .
  eq isLhsTerm((QT : QTNeList)) = isLhsTerm(QT) andBool isLhsTerm(QTNeList) .
  eq isLhsTerm(.emptyTermList) = true .
  eq isLhsTerm(QC)
   = substrString(string(getType(QC)), 0, 2) =/=Bool "PE" .
 ceq isLhsTerm(Q[QTList]) = false
  if substrString(string(Q), 0, 1) ==Bool "?" orBool Q ==Bool 'returns_ .
 ceq isLhsTerm(Q[QTList]) = true
  if Q ==Bool 'skolem .
  eq isLhsTerm(Q[QTList]) = isLhsTerm(QTList) [owise] .


  var N : Nat .
  var Subst : Subst .
  var SubstItem : SubstItem .
  eq filterAnonymousFreeSubst(FreeInt(N) <- Obj) = .Subst .
  eq filterAnonymousFreeSubst(FreeSeq(N) <- Obj) = .Subst .
  eq filterAnonymousFreeSubst(FreeMSet(N) <- Obj) = .Subst .
  eq filterAnonymousFreeSubst(FreeTree(N) <- Obj) = .Subst .
  eq filterAnonymousFreeSubst(skolem(N, PEObj) <- Obj) = .Subst .
  eq filterAnonymousFreeSubst(SubstItem) = SubstItem [owise] .

  var List : List .
  var Bag : Bag .
  var Map : Map .
  var Var VarRoot : String .
 ceq filterFrame!Subst((!ListItem(Var) <- List) ;; Subst, VarRoot)
   = filterFrame!Subst(Subst, VarRoot)
  if substrString(Var,
                  lengthString(Var) -Int lengthString(VarRoot),
                  lengthString(VarRoot))
     ==Bool VarRoot .
 ceq filterFrame!Subst((!BagItem(Var) <- Bag) ;; Subst, VarRoot)
   = filterFrame!Subst(Subst, VarRoot)
  if substrString(Var,
                  lengthString(Var) -Int lengthString(VarRoot),
                  lengthString(VarRoot))
     ==Bool VarRoot .
 ceq filterFrame!Subst((!MapItem(Var) <- Map) ;; Subst, VarRoot)
   = filterFrame!Subst(Subst, VarRoot)
  if substrString(Var,
                  lengthString(Var) -Int lengthString(VarRoot),
                  lengthString(VarRoot))
     ==Bool VarRoot .
  eq filterFrame!Subst(SubstItem ;; Subst, VarRoot)
   = SubstItem ;; filterFrame!Subst(Subst, VarRoot) [owise] .
  eq filterFrame!Subst(.Subst, VarRoot) = .Subst .

  var FreeListItem : FreeListItem .
  var FreeBagItem : FreeBagItem .
  var FreeMapItem : FreeMapItem .
  eq filterCallCoSkolemSubst((co-skolem(N, FreeObj) <- Obj) ;; Subst)
   = filterCallCoSkolemSubst(Subst) .
  eq filterCallCoSkolemSubst((co-skolem(N, FreeListItem) <- List) ;; Subst)
   = filterCallCoSkolemSubst(Subst) .
  eq filterCallCoSkolemSubst((co-skolem(N, FreeBagItem) <- Bag) ;; Subst)
   = filterCallCoSkolemSubst(Subst) .
  eq filterCallCoSkolemSubst((co-skolem(N, FreeMapItem) <- Map) ;; Subst)
   = filterCallCoSkolemSubst(Subst) .
  eq filterCallCoSkolemSubst(SubstItem ;; Subst)
   = SubstItem ;; filterCallCoSkolemSubst(Subst) [owise] .
  eq filterCallCoSkolemSubst(.Subst) = .Subst . 
endm
